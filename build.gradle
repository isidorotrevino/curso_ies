buildscript {
    dependencies {
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.github.jruby-gradle.base' version '1.4.0'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.asciidoctor.convert'

version = '1.0.0'

ext {
    deckjsVersion = 'master'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    deckjsDir   = new File(downloadDir,'deck.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:haml:4.0.7'
}

task download {
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-deck.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-deck.js-${asciidoctorBackendVersion}/templates",
            templateDir, recursive:true, overwrite:true
            cp "zip:https://github.com/imakewebthings/deck.js/archive/${deckjsVersion}.zip!deck.js-${deckjsVersion}",
            deckjsDir, recursive:true, overwrite:true
        }
    }
}

download {
    description "Download extra deckjs resources"
    outputs.dir templateDir
    outputs.dir deckjsDir
}

asciidoctorj {
    version = '1.5.5'
}

asciidoctor {
    dependsOn jrubyPrepare

    sources {
		include '*.adoc'
    }

    resources {
        from (sourceDir) {
            include 'images/**'
            include '**/*'
        }
        from (downloadDir) {
            include 'deck.js/**'
        }
    }

    backends 'html5'

    attributes \
		'deckjs_theme':'swiss',
		'deckjs_transition':'horizontal-slide',
		'stylesdir':'deck.js/themes/style',
		 encoding: 'utf-8',
		'navigation':'',
		'goto':'',
		'menu':'',
		'status':'',
		'split':'',
		'blank':'',		
		'viewport':'width=1000, user-scalable=yes',
		'sectids!':'',
		'showtitle':'',
        'build-gradle': file('build.gradle'),
        'sourcedir': project.sourceSets.main.java.srcDirs[0],
        'endpoint-url': 'http://example.org',
        'source-highlighter': 'coderay',		
        'imagesdir': './images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': ''

    options template_dirs : [new File(templateDir,'haml').absolutePath ]

    dependsOn download
}
